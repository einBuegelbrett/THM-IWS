package block_5._11_EigeneTasks

plugins {
    id 'groovy'
}

group = 'de.thm'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

/**
 * In dieser Übung erstellt ihr eigene Gradle-Tasks, um den Umgang
 * mit dem Gradle DSL (Groovy-basiert) praktisch zu üben.
 *
 * Testet eure Tasks mit:
 * ./gradlew greetWorkshop
 * ./gradlew createInfoFile
 * ./gradlew processNumbers
 *
 * Optional:
 * ./gradlew tasks
 * ./gradlew help --task <taskname>
 */

/**
 * Task 1 - Leicht: Begrüßungstask
 * Erstellt einen Task mit dem Namen: greetWorkshop
 * Anforderungen:
 *      - Gibt eine Begrüßung im Terminal aus
 *      - Nutzt mindestens einen GString
 *      - Hat group = "Workshop" und eine Beschreibung
 *
 * Beispielausgabe:
 * Willkommen zum Groovy-Gradle Workshop!
 * Projekt: <Projektname>
 */
tasks.register('greetWorkshop') {
    group = "Workshop"
    description = "Gibt eine Begrüßung für den Workshop aus"

    doLast {
        println "Willkommen zum Groovy-Gradle Workshop!"
        println "Projekt: $project.name"
        println "Version: $project.version"
    }
}

/**
 * Task 2 – Mittel: Dateierstellung
 * Erstellt einen Task: createInfoFile
 * Anforderungen:
 *      - Erstellt eine Datei build/info.txt
 *      - Schreibt folgende Infos hinein:
 *          - Projektname
 *          - Version
 *          - Aktuelles Datum
 *      - Nutzt Gradle/Groovy APIs zum Schreiben der Datei
 *      - Nutzt mindestens eine Variable im Text (GString)
 * Bonus (optional):
 * Task soll als UP-TO-DATE erkannt werden, wenn sich nichts ändert
 */
tasks.register('createInfoFile') {
    group = "Workshop"
    description = "Erstellt eine Info-Datei mit Projektmetadaten"

    doLast {
        def outputFile = file("$layout.buildDirectory/info.txt")

        outputFile.parentFile.mkdirs()

        def content = """
            Projekt: $project.name
            Version: $project.version
            Datum: ${new Date()}
            """

        outputFile.text = content
        println "Datei erstellt: $outputFile"
    }
}

/**
 * Task 3 – Schwer: Task-Pipeline
 * Erstellt zwei neue Tasks und verknüpft sie: generateNumbers, processNumbers
 * generateNumbers:
 *      - Erstellt Datei build/numbers.txt
 *      - Inhalt: Zahlen 1–10 (eine pro Zeile)
 *      - Nutzt eine Groovy Range (1..10)
 * processNumbers:
 *      - Liest numbers.txt
 *      - Filtert nur gerade Zahlen
 *      - Schreibt Ergebnis in build/evenNumbers.txt
 * Wichtig:
 *      - processNumbers soll automatisch generateNumbers ausführen
 *      - Nutzt dependsOn
 */

// Generiert Zahlen 1–10 in Datei
tasks.register('generateNumbers') {
    group = "Workshop"
    description = "Erstellt Datei mit Zahlen von 1 bis 10"

    doLast {
        def file = file("$layout.buildDirectory/numbers.txt")

        file.parentFile.mkdirs()

        def numbers = (1..10).join("\n")

        file.text = numbers

        println "numbers.txt erstellt."
    }
}

// Verarbeitet Zahlen → nur gerade
tasks.register('processNumbers') {
    group = "Workshop"
    description = "Filtert gerade Zahlen aus Datei"
    dependsOn 'generateNumbers'

    doLast {
        def inputFile  = file("$layout.buildDirectory/numbers.txt")

        def outputFile = file("$layout.buildDirectory/evenNumbers.txt")

        def numbers = inputFile.readLines()

        def evenNumbers = numbers
                .collect { it as Integer }
                .findAll { it % 2 == 0 }

        outputFile.text = evenNumbers.join("\n")

        println "Gerade Zahlen gespeichert in evenNumbers.txt"
    }
}