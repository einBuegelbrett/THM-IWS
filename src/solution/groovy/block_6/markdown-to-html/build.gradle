plugins {
    id 'groovy'
    id 'application'
}

group = 'de.thm.workshop'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.groovy:groovy:5.0.0'
}

application {
    mainClass = 'App'
}

tasks.register('markdownToHtml') {
    group = "Markdown"
    description = "Converts Markdown files into groovy HTML ðŸ•º"

    doLast {

        def inputDir = file("input")
        def outputDir = file("output")

        if (!inputDir.exists()) {
            println "Input directory not found: $inputDir"
            return
        }

        outputDir.mkdirs()

        def converter = new MarkdownConverter()

        def files = inputDir.listFiles()
                .findAll { it.name.endsWith(".md") }

        if (!files) {
            println "No Markdown files found."
            return
        }

        println "Converting ${files.size()} file(s)..."

        files.each { file ->

            println "ðŸŽ¶ Processing: $file.name"

            def text = file.text
            def html = converter.convert(text)

            def outputFile =
                    new File(outputDir, file.name.replace(".md", ".html"))

            outputFile.text = html
        }

        println "Conversion finished. Stayin' Alive!"
    }
}
